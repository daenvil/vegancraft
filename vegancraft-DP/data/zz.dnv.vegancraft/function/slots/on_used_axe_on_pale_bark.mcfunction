advancement revoke @s only zz.dnv.vegancraft:triggers/used_axe_on_pale_bark
scoreboard players set @s dnv.resin_from_stripping.n_drops 0
execute if predicate {condition:"random_chance",chance:0.75} if predicate {condition:"any_of",terms:[{condition:"entity_properties",entity:"this",predicate:{slots:{weapon.mainhand:{items:"#axes",predicates:{enchantments:[{enchantment:"minecraft:fortune",levels:3}]}}}}},{condition:"entity_properties",entity:"this",predicate:{slots:{weapon.offhand:{items:"#axes",predicates:{enchantments:[{enchantment:"minecraft:fortune",levels:3}]}}}}}]} run scoreboard players set @s dnv.resin_from_stripping.n_drops 1
execute unless score @s dnv.resin_from_stripping.n_drops matches 1 if predicate {condition:"random_chance",chance:0.5} if predicate {condition:"any_of",terms:[{condition:"entity_properties",entity:"this",predicate:{slots:{weapon.mainhand:{items:"#axes",predicates:{enchantments:[{enchantment:"minecraft:fortune",levels:2}]}}}}},{condition:"entity_properties",entity:"this",predicate:{slots:{weapon.offhand:{items:"#axes",predicates:{enchantments:[{enchantment:"minecraft:fortune",levels:2}]}}}}}]} run scoreboard players set @s dnv.resin_from_stripping.n_drops 1
execute unless score @s dnv.resin_from_stripping.n_drops matches 1 if predicate {condition:"random_chance",chance:0.25} if predicate {condition:"any_of",terms:[{condition:"entity_properties",entity:"this",predicate:{slots:{weapon.mainhand:{items:"#axes",predicates:{enchantments:[{enchantment:"minecraft:fortune",levels:1}]}}}}},{condition:"entity_properties",entity:"this",predicate:{slots:{weapon.offhand:{items:"#axes",predicates:{enchantments:[{enchantment:"minecraft:fortune",levels:1}]}}}}}]} run scoreboard players set @s dnv.resin_from_stripping.n_drops 1
execute if predicate {condition:"random_chance",chance:0.25} run scoreboard players add @s dnv.resin_from_stripping.n_drops 1
execute if score @s dnv.resin_from_stripping.n_drops matches 1.. run function zz.dnv.vegancraft:find_pale_bark